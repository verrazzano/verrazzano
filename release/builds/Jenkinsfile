// Copyright (c) 2021, 2022, Oracle and/or its affiliates.
// Licensed under the Universal Permissive License v 1.0 as shown at https://oss.oracle.com/licenses/upl.

def DOCKER_IMAGE_TAG
def releaseBuild
def RELEASE_JOB_NAME
def RELEASE_BRANCH_COMMIT
def IS_PATCH_RELEASE = false

pipeline {
    options {
        skipDefaultCheckout true
        timestamps ()
    }

    agent {
       docker {
            image "${RELEASE_RUNNER_IMAGE}"
            args "${RELEASE_RUNNER_DOCKER_ARGS}"
            registryUrl "${RUNNER_DOCKER_REGISTRY_URL}"
            registryCredentialsId 'ocir-pull-and-push-account'
            label "internal"
        }
    }

    parameters {
        string (description: 'The location of an existing candidate images file to use. This is provided by automation in the normal flow. The default NONE indicates a full build/test is required', name: 'IMAGES_TO_PUBLISH_OBJECT_STORE_FILE', defaultValue: 'NONE', trim: true)
        string (description: 'The source commit for the release (required for full release)', name: 'COMMIT_TO_USE', defaultValue: 'NONE', trim: true )
        string (description: 'The target version (major.minor.patch format, e.g. 1.0.1)', name: 'TARGET_VERSION', defaultValue: 'NONE', trim: true)
        string (description: 'Number of times to retry a failed triggered build', name: 'BUILD_RETRIES', defaultValue: '1', trim: true)
        booleanParam (description: 'Ignore pre-release validation failures', name: 'IGNORE_PRE_RELEASE_VALIDATION_FAILURES', defaultValue: false)
        booleanParam (description: 'Only create release branch (skips release validation, build, and image push)', name: 'CREATE_BRANCH_ONLY', defaultValue: false)
        booleanParam (description: 'Whether to copy the product zip into the release object store from the last clean periodic test',
                name: 'COPY_PRODUCT_ZIP_FROM_PERIODIC', defaultValue: false)
        booleanParam (description: 'Indicate whether this is a test run', name: 'TEST_RUN', defaultValue: false)
        string (name: 'VERRAZZANO_HELPER_BRANCH',
                        defaultValue: 'master',
                        description: 'verrazzano-helper branch. master is used for 1.3+, release-1.2 is used for 1.2 and earlier, user branch name is used when testing verrazzano-helper changes',
                        trim: true)
    }

    environment {
        OCR_CREDS = credentials('ocr-pull-and-push-account')
        NETRC_FILE = credentials('netrc')
        DOCKER_CREDS = credentials('github-packages-credentials-rw')
        DOCKER_REPO = 'ghcr.io'
        GITHUB_CREDENTIALS = credentials('github_rw')

        TIMESTAMP = sh(returnStdout: true, script: "date +%Y%m%d%H%M%S").trim()

        OCI_OS_NAMESPACE = credentials('oci-os-namespace')
        OCI_OS_BUCKET="verrazzano-builds"
        OCI_OS_REGION="us-phoenix-1"
        OCI_OS_SHARED_BUCKET="build-shared-files"
        OCI_CLI_AUTH="api_key"
        OCI_CLI_TENANCY = credentials('oci-tenancy')
        OCI_CLI_USER = credentials('oci-user-ocid')
        OCI_CLI_FINGERPRINT = credentials('oci-api-key-fingerprint')
        OCI_CLI_KEY_FILE = credentials('oci-api-key')

        CLEAN_BRANCH_NAME = "${env.BRANCH_NAME.replace("/", "%2F")}"
        PERIODIC_JOB_OBJECT_PREFIX = "${CLEAN_BRANCH_NAME}-last-clean-periodic-test"
        PERIODIC_PRODUCT_LITE_OBJECT_NAME = "verrazzano-${params.TARGET_VERSION}-lite.zip"
        PERIODIC_PRODUCT_FULL_OBJECT_NAME = "verrazzano-${params.TARGET_VERSION}.zip"
    }

    stages {
        stage('Clean workspace and checkout') {
            steps {
                sh """
                    echo "${NODE_LABELS}"
                """
                script {
                    if (params.COMMIT_TO_USE == "NONE") {
                        echo "Specific GIT commit was not specified, use current head"
                        def scmInfo = checkout scm
                        env.GIT_COMMIT = scmInfo.GIT_COMMIT
                        env.GIT_BRANCH = scmInfo.GIT_BRANCH
                    } else {
                        echo "SCM checkout of ${params.COMMIT_TO_USE}"
                        def scmInfo = checkout([
                            $class: 'GitSCM',
                            branches: [[name: params.COMMIT_TO_USE]],
                            doGenerateSubmoduleConfigurations: false,
                            extensions: [],
                            submoduleCfg: [],
                            userRemoteConfigs: [[url: env.SCM_VERRAZZANO_GIT_URL]]])
                        env.GIT_COMMIT = scmInfo.GIT_COMMIT
                        env.GIT_BRANCH = scmInfo.GIT_BRANCH
                        // If the commit we were handed is not what the SCM says we are using, fail
                        if (!env.GIT_COMMIT.equals(params.COMMIT_TO_USE)) {
                            echo "SCM didn't checkout the commit we expected. Expected: ${params.COMMIT_TO_USE}, Found: ${env.GIT_COMMIT}"
                            sh "exit 1"
                        }
                    }
                    // setup credential retrieval for possible release branch push
                    sh """
                        git config credential.https://github.com.username ${GITHUB_CREDENTIALS_USR}
                        git config credential.helper '/bin/bash ${WORKSPACE}/release/scripts/credential_helper.sh'
                    """
                    echo "SCM checkout of ${env.GIT_BRANCH} at ${env.GIT_COMMIT}"
                    RELEASE_BRANCH_COMMIT = env.GIT_COMMIT
                }

                script {
                    def props = readProperties file: '.verrazzano-development-version'
                    VERRAZZANO_DEV_VERSION = props['verrazzano-development-version']
                    TIMESTAMP = sh(returnStdout: true, script: "date +%Y%m%d%H%M%S").trim()
                    SHORT_COMMIT_HASH = sh(returnStdout: true, script: "git rev-parse --short=8 HEAD").trim()
                    // update the description with some meaningful info
                    currentBuild.description = SHORT_COMMIT_HASH + " : " + env.GIT_COMMIT + " : " + params.COMMIT_TO_USE
                }
                script {
                    sh """
                        echo "Downloading verrazzano-helper from object storage"
                        oci --region us-phoenix-1 os object get --namespace ${OCI_OS_NAMESPACE} -bn ${OCI_OS_SHARED_BUCKET} --name ${params.VERRAZZANO_HELPER_BRANCH}/verrazzano-helper --file ${WORKSPACE}/verrazzano-helper
                        chmod uog+x ${WORKSPACE}/verrazzano-helper
                    """
                }
            }
        }

        stage('Pipeline inputs validation') {
            steps {
                script {
                    // major or minor release has a target version that ends in 0
                    if (params.TARGET_VERSION =~ /^\d+\.\d+\.0$/) {
                        echo "major/minor release detected. test=${params.TEST_RUN}"
                        IS_PATCH_RELEASE = 'false'
                    // patch should be using a "release-#.#" branch and have a version ending in a digit other than 0
                    } else if (env.BRANCH_NAME =~ /release-\d+\.\d+$/ && params.TARGET_VERSION =~ /^\d+\.\d+\.[1-9]+$/){
                        echo "patch release detected"
                        IS_PATCH_RELEASE = 'true'
                    } else if (params.TEST_RUN && env.BRANCH_NAME =~ /mock-release-\d+\.\d+$/ && params.TARGET_VERSION =~ /^\d+\.\d+\.[1-9]+$/){
                        echo "test patch release detected"
                        IS_PATCH_RELEASE = 'true'
                    } else {
                        error "Invalid source branch ${env.GIT_BRANCH} or a mismatch between source branch and the specified target version ${params.TARGET_VERSION}"
                    }

                    // if CREATE_BRANCH_ONLY is checked, make sure the source branch is master
                    if (!params.TEST_RUN && params.CREATE_BRANCH_ONLY && env.BRANCH_NAME != "master") {
                        error "Release branch must be created from master, ${env.BRANCH_NAME} is an invalid source branch"
                    }

                    echo "Patch release? ${IS_PATCH_RELEASE}"
                }
            }
        }

        stage('Pre-release validation') {
            when {
                expression { !params.CREATE_BRANCH_ONLY }
            }
            environment {
                IGNORE_FAILURES = "${params.IGNORE_PRE_RELEASE_VALIDATION_FAILURES}"
                TICKET_SERVICE_USERNAME = credentials('ticket-service-username')
                TICKET_SERVICE_PASSWORD = credentials('ticket-service-password')
            }
            steps {
                script {
                    sh """
                        cd ${WORKSPACE}
                        ./release/scripts/prerelease_validation.sh ${params.TARGET_VERSION}
                    """
                }
            }
        }

        stage('Trigger release build') {
            // REVIEW: The current iteration, we are releasing candidates from release-* branches which exist
            // this can be extended in the future to allow release from master which would create the branch and then use an existing
            // candidate from master, but for now we aren't adding that complexity and keeping this simple on (not crossing branches yet)
            when {
                expression { return params.IMAGES_TO_PUBLISH_OBJECT_STORE_FILE == 'NONE' }
            }
            stages {
                stage('Release Only: Create branch') {
                    // if this is not a patch there is a need to either create a release branch or verify it exists
                    when {
                        expression { IS_PATCH_RELEASE == 'false' }
                    }
                    environment {
                        TEST_RUN = "${params.TEST_RUN}"
                    }
                    steps {
                        script {
                            sh """
                                cd ${WORKSPACE}
                                ./release/scripts/create_branch.sh -c ${env.GIT_COMMIT} -v ${params.TARGET_VERSION}
                            """
                        }
                    }

                }

                stage('Verrazzano Build') {
                    steps {
                        script {
                            if (IS_PATCH_RELEASE == 'true') {
                                RELEASE_JOB_NAME = 'verrazzano/' + env.BRANCH_NAME
                            } else {
                                def parts = params.TARGET_VERSION.split("\\.")
                                def MAJOR=parts[0]
                                def MINOR=parts[1]
                                if ( params.TEST_RUN ) {
                                    RELEASE_JOB_NAME = "verrazzano/mock-release-${MAJOR}.${MINOR}"
                                } else {
                                    RELEASE_JOB_NAME = "verrazzano/release-${MAJOR}.${MINOR}"
                                }
                            }
                            // if branch build not found, trigger repo scan and wait
                            echo "Searching for ${RELEASE_JOB_NAME}"
                            // if branch build not found, trigger repo scan and wait
                            if (Jenkins.instance.getItemByFullName(RELEASE_JOB_NAME) == null) {
                                echo "Build not found. invoking schedule build"
                                Jenkins.instance.getItemByFullName('verrazzano').scheduleBuild()
                                while (Jenkins.instance.getItemByFullName(RELEASE_JOB_NAME) == null) {
                                    sleep(1)
                                }
                            }
                            echo "Release build exists"
                        }

                        retry(count: env.BUILD_RETRIES) {
                            script {
                                echo "Triggering Verrazzano build for release, this will wait for triggered tests to finish"
                                releaseBuild = build job: "${RELEASE_JOB_NAME}",
                                    parameters: [
                                        booleanParam(name: 'RUN_ACCEPTANCE_TESTS', value: true),
                                        booleanParam(name: 'RUN_SLOW_TESTS', value: true),
                                        booleanParam(name: 'TRIGGER_FULL_TESTS', value: true),
                                        booleanParam(name: 'WAIT_FOR_TRIGGERED', value: true),
                                        booleanParam(name: 'GENERATE_TARBALL', value: true),
                                        booleanParam(name: 'GENERATE_CLI', value: true),
                                        booleanParam(name: 'PUSH_TO_OCIR', value: true)
                                    ], wait: true
                            }
                        }
                    }
                    post {
                        success {
                            script {
                                // keep the successful build forever
                                releaseBuild.getRawBuild().keepLog(true)
                            }
                        }
                    }
                }
            }
        }

        stage('Copy Product Zip to Release') {
            when {
                // When the Verrazzano builds are not run as part of this release job, the GENERATE_TARBALL which
                // creates the product zip in the release location is not run. This is usually the case when we are using
                // an existing release candidate, for which periodic tests have already run and identified a release
                // candidate. In this case, we should copy the product zip generated by the periodic tests, into the
                // release location so that stage 2 release job has access to it. Also copy the commit file similarly.
                expression { params.COPY_PRODUCT_ZIP_FROM_PERIODIC }
            }
            steps {
                script {
                    // NOTE - this copy operation is asynchronous. The assumption here is that it will complete
                    // by the time the images are pushed to OCR (next build stage), and the release stage2, which uses
                    // these objects, is run.
                    sh """
                        oci --region ${OCI_OS_REGION} os object copy --namespace ${OCI_OS_NAMESPACE} \
                          -bn ${OCI_OS_BUCKET} --destination-bucket ${OCI_OS_BUCKET} \
                          --source-object-name ${PERIODIC_JOB_OBJECT_PREFIX}/${PERIODIC_PRODUCT_LITE_OBJECT_NAME} \
                          --destination-object-name ${CLEAN_BRANCH_NAME}/${PERIODIC_PRODUCT_LITE_OBJECT_NAME}

                        oci --region ${OCI_OS_REGION} os object copy --namespace ${OCI_OS_NAMESPACE} \
                          -bn ${OCI_OS_BUCKET} --destination-bucket ${OCI_OS_BUCKET} \
                          --source-object-name ${PERIODIC_JOB_OBJECT_PREFIX}/${PERIODIC_PRODUCT_LITE_OBJECT_NAME}.sha256 \
                          --destination-object-name ${CLEAN_BRANCH_NAME}/${PERIODIC_PRODUCT_LITE_OBJECT_NAME}.sha256

                        oci --region ${OCI_OS_REGION} os object copy --namespace ${OCI_OS_NAMESPACE} \
                          -bn ${OCI_OS_BUCKET} --destination-bucket ${OCI_OS_BUCKET} \
                          --source-object-name ${PERIODIC_JOB_OBJECT_PREFIX}/${PERIODIC_PRODUCT_FULL_OBJECT_NAME} \
                          --destination-object-name ${CLEAN_BRANCH_NAME}/${PERIODIC_PRODUCT_FULL_OBJECT_NAME}

                        oci --region ${OCI_OS_REGION} os object copy --namespace ${OCI_OS_NAMESPACE} \
                          -bn ${OCI_OS_BUCKET} --destination-bucket ${OCI_OS_BUCKET} \
                          --source-object-name ${PERIODIC_JOB_OBJECT_PREFIX}/${PERIODIC_PRODUCT_FULL_OBJECT_NAME}.sha256 \
                          --destination-object-name ${CLEAN_BRANCH_NAME}/${PERIODIC_PRODUCT_FULL_OBJECT_NAME}.sha256

                        oci --region ${OCI_OS_REGION} os object copy --namespace ${OCI_OS_NAMESPACE} \
                          -bn ${OCI_OS_BUCKET} --destination-bucket ${OCI_OS_BUCKET} \
                          --source-object-name ${PERIODIC_JOB_OBJECT_PREFIX}/verrazzano_periodic-commit.txt \
                          --destination-object-name ${CLEAN_BRANCH_NAME}/verrazzano_${params.TARGET_VERSION}-commit.txt
                    """
                }
            }
        }

        stage('Push images to OCR') {
            when {
                allOf {
                    expression { !params.TEST_RUN }
                    expression { !params.CREATE_BRANCH_ONLY }
                }
            }
            steps {
                retry(count: env.BUILD_RETRIES) {
                    script {
                        if (params.IMAGES_TO_PUBLISH_OBJECT_STORE_FILE.equals("NONE")) {
                            echo "Triggering OCR Image push build"
                            build job: "publish-release-images-to-ocr",
                                parameters: [
                                    string(name: 'IMAGES_TO_PUBLISH_JOB_NAME', value: "${RELEASE_JOB_NAME}"),
                                ], wait: true
                        } else {
                            echo "Triggering OCR Image push build supplying existing candidate"
                            build job: "publish-release-images-to-ocr",
                                parameters: [
                                    string(name: 'IMAGES_TO_PUBLISH_OBJECT_STORE_FILE', value: "${params.IMAGES_TO_PUBLISH_OBJECT_STORE_FILE}"),
                                ], wait: true
                        }
                    }
                }
            }
        }
    }
}
